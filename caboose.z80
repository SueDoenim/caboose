#include "ti83plus.inc"

.org $4000

.db $80,$0F,0,0,0,0 ;App header
.db $80,$12,$01,$04
.db $80,$47,"Caboose"
.db $80,$81,1
.db $80,$90
.db $03,$22,$09,$00
.db $80,$70

setHomescreenHook:
  ld hl,homescreenHook ;set the homecreen hook
  ld (homescreenHookPtr),hl
  in a,($06)
  ld (homescreenHookPtr + 2),a
  set homescreenHookActive,(iy + hookflags2)
  bjump(_JForceCmdNoChar)

homescreenHook:
.db $83
  dec a ;check why the hook was called
  jr z,keypress

retZ:
  cp a
  ret

keypress:
  ld a,b ;check if Enter was pressed
  cp kEnter
  jr z,checkIfEntry
  
  cp a
  ret

checkIfEntry:
  bit 3,(iy + $49) ;check if an entry is being recalled
  jr nz,retZ

checkCursor:
  ld hl,(editCursor) ;check if cursor is over empty Mathprint box
  ld a,(hl)
  cp $EF
  jr nz,checkIfAtEnd
  inc hl
  ld a,(hl)
  cp $1E
  jr z,setGetCSCHook

checkIfAtEnd:
  ld hl,(editBtm) ;check if cursor is at end of buffer
  ld de,(editTail)
  or a
  sbc hl,de
  jr nz,retZ

checkToken:
  ld hl,(editCursor) ;check what the token before the cursor is
  dec hl
  dec hl
  ld a,(hl) ;check if it's a 2-byte token
  cp $BB
  jr z,miscToken
  cp $EF
  jr z,TI84Token
  ld hl,TwoByteTokens
  ld bc,TwoByteTokensEnd-TwoByteTokens
  cpir
  ld b,kEnter
  ret z

checkOneByteToken:
  ld hl,(editCursor) ;check 1-byte tokens
  dec hl
  ld a,(hl)
  ld hl,OneByteTokens
  ld bc,OneByteTokensEnd-OneByteTokens
  cpir
  ld b,kEnter
  jr z,setGetCSCHook
  cp a
  ret

miscToken:
  ld hl,(editCursor) ;check miscellaneous 2-byte tokens
  dec hl
  ld a,(hl)
  ld hl,miscTokens
  ld bc,miscTokensEnd-miscToken
  ldir
  ld b,kEnter
  jr z,setGetCSCHook
  cp a
  ret

TI84Token:
  ld hl,(editCursor) ;check ti-84 only tokens
  dec hl
  ld a,(hl)
  ld hl,TI84Tokens
  ld bc,TI84TokensEnd-TI84Tokens
  cpir
  ld b,kEnter
  jr z,setGetCSCHook
  cp a
  ret

setGetCSCHook:
  ld hl,flags + hookflags2 ;set getCSC hook to press enter
  ld de,appBackUpScreen
  ld a,(hl)
  ld (de),a
  or %00000001
  ld (hl),a
  ld hl,getKeyHookPtr
  inc e
  ldi
  ldi
  ldi
  ld hl,getCSCHook
  ld (getKeyHookPtr),hl
  in a,($06)
  ld (getKeyHookPtr + 2),a
  ld b,kAns ;return Ans
  cp a
  ret

getCSCHook:
.db $83
  ld hl,appBackUpScreen ;restore previous hook
  ld de,flags + hookflags2
  ldi
  ld de,getKeyHookPtr
  ldi
  ldi
  ldi
  ld a,kEnter ;return Enter
  cp a
  ret

TwoByteTokens:
.db tVarMat,tVarLst,tVarEqu,tVarPict,tVarGDB,tVarStrng,tVarOut,tVarSys,tGFormat
TwoByteTokensEnd:

OneByteTokens:
.db tLBrack,tLBrace,tLParen,tRound,tMax,tMin,tMedian,tMean,tComma,tOr,tXor,tAnd
.db tEQ,tLT,tGT,tLE,tGE,tNE,tAdd,tSub,tFix,tMul,tDiv,tnPr,tnCr,tYOn,tYOff
.db tStPic,tRcPic,tStoDB,tRclDB,tVert,tHorz,tDrInv,tDrawF,tChs,tInt,tAbs,tDet
.db tIdent,tDim,tSum,tProd,tNot,tIPart,tFPart,tSqrt,tCubRt,tLn,tExp,tLog,tALog
.db tSin,tASin,tCos,tACos,tTan,tATan,tSinH,tASinH,tCoshH,tACosH,tSortA,tSortD
.db tPower,tXRoot
OneByteTokensEnd:

miscTokens:
.db tFinBal,tSubStrng,tStdDev,tVariance,tInvNorm,tNormalPDF,tConj,tReal,tImag
.db tAngle,tCumSum,tExpr,tLength,tDeltaLst,tRef,tRRef
miscTokensEnd:

TI84Tokens:
.db $02,$03,$04,$05,$07,$08,$67,$96,$97,$98
TI84TokensEnd:
